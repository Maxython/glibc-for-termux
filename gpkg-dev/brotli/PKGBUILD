# The original PKGBUILD was taken from the archlinux/svntogit-packages repo:
# - https://github.com/archlinux/svntogit-packages/blob/packages/brotli/trunk/PKGBUILD

pkgname=brotli-glibc
_gitcommit=e61745a6b7add50d380cfd7d3883dd6c62fc2c71
pkgver=1.0.9
pkgrel=0
pkgdesc='Generic-purpose lossless compression algorithm'
url='https://github.com/google/brotli'
arch=(any)
license=('MIT')
makedepends=('git' 'glibc' 'gcc-libs' 'cmake' 'python-setuptools')
source=(brotli::"git+${url}#commit=${_gitcommit}")
sha512sums=('SKIP')
groups=('gpkg-dev')

prepare() {
  cd brotli
  git cherry-pick -n 09b0992b6acb7faa6fd3b23f9bc036ea117230fc # Fix broken Libs: in pc file
}

pkgver() {
  cd brotli
  git describe --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd brotli

  python setup.py build
  cmake -B build \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=$GLIBC_PREFIX \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=True \
    -DCMAKE_C_FLAGS="$CFLAGS -ffat-lto-objects"
  cmake --build build -v
}

package() {
  cd brotli
  DESTDIR="$pkgdir" cmake --install build
  local man;
  for man in docs/*.?; do
    install -Dm 644 "$man" "$pkgdir/$GLIBC_PREFIX/share/man/man${man##*.}/${man##*/}"
  done
  install -Dm 644 LICENSE -t "$pkgdir/$GLIBC_PREFIX/share/licenses/$pkgname"
}
