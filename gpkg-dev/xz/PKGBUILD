# The original PKGBUILD was taken from the gitlab.archlinux.org/archlinux/packaging repo:
# - https://gitlab.archlinux.org/archlinux/packaging/packages/xz/-/blob/main/PKGBUILD

pkgname=xz-glibc
pkgver=5.4.3
pkgrel=0
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('any')
url='https://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
source=("https://tukaani.org/xz/xz-${pkgver}.tar.gz")
sha256sums=('1c382e0bc2e4e0af58398a903dd62fff7e510171d2de47a1ebe06d1528e9b7e9')
sha512sums=('aff0fe166af6df4491a6f5df2372cab100b081452461a0e8c6fd65b72af3f250f16c64d9fb8fd309141e9b9ae4e41649f48687cc29e63dd82f27f2eab19b4023')
groups=('gpkg-dev')

build() {
  cd "${srcdir}/xz-${pkgver}"

  ./configure \
    --prefix=$GLIBC_PREFIX \
    --disable-rpath \
    --enable-werror
  make
}

package() {
  cd "${srcdir}/xz-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -d -m0755 "${pkgdir}/$GLIBC_PREFIX/share/licenses/xz/"
  ln -sf $GLIBC_PREFIX/share/doc/xz/COPYING "${pkgdir}/$GLIBC_PREFIX/share/licenses/xz/"
  ln -sf $GLIBC_PREFIX/share/licenses/common/GPL2/license.txt "${pkgdir}/$GLIBC_PREFIX/share/doc/xz/COPYING.GPLv2"
}
