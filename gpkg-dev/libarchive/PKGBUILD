# The original PKGBUILD was taken from the gitlab.archlinux.org/archlinux/packaging repo:
# - https://gitlab.archlinux.org/archlinux/packaging/packages/libarchive/-/blob/main/PKGBUILD

pkgname=libarchive-glibc
pkgver=3.7.1
pkgrel=0
pkgdesc='Multi-format archive and compression library (lite version)'
arch=(any)
url='https://libarchive.org/'
license=('BSD')
source=("https://github.com/libarchive/libarchive/releases/download/v${pkgver}/libarchive-${pkgver}.tar.xz")
sha256sums=('b17403ce670ff18d8e06fea05a9ea9accf70678c88f1b9392a2e29b51127895f')
groups=('gpkg-dev')

build() {
  cd "libarchive-${pkgver}"

  ./configure \
      --host=$GPKG_DEV_TARGET \
      --build=$GPKG_DEV_TARGET \
      --target=$GPKG_DEV_TARGET \
      --prefix=$GLIBC_PREFIX \
      --without-xml2 \
      --without-nettle \
      --disable-bsdtar \
      --disable-bsdcat \
      --disable-bsdcpio \
      --disable-static
  make
}

package() {
  cd "libarchive-${pkgver}"

  make DESTDIR="$pkgdir" install
  sed -i "s/iconv //" "$pkgdir"/$GLIBC_PREFIX/lib/pkgconfig/libarchive.pc
  install -Dm0644 COPYING "$pkgdir/$GLIBC_PREFIX/share/licenses/libarchive/COPYING"
  rm -fr "$pkgdir/$GLIBC_PREFIX/glibc/bin"
}
