# The original PKGBUILD was taken from the archlinux/svntogit-packages repo:
# - https://github.com/archlinux/svntogit-packages/blob/packages/libarchive/trunk/PKGBUILD

pkgname=libarchive-glibc
pkgver=3.6.2
pkgrel=0
pkgdesc='Multi-format archive and compression library (lite version)'
arch=(any)
url='https://libarchive.org/'
license=('BSD')
#depends=('acl' 'libacl.so' 'bzip2' 'expat' 'lz4' 'openssl' 'libcrypto.so' 'xz'
#         'zlib' 'zstd')
provides=('libarchive.so')
source=("https://github.com/libarchive/libarchive/releases/download/v${pkgver}/libarchive-${pkgver}.tar.xz")
sha256sums=('9e2c1b80d5fbe59b61308fdfab6c79b5021d7ff4ff2489fb12daf0a96a83551d')
groups=('gpkg-dev')

build() {
  cd "libarchive-${pkgver}"

  ./configure \
      --prefix=$GLIBC_PREFIX \
      --without-xml2 \
      --without-nettle \
      --disable-bsdtar \
      --disable-bsdcat \
      --disable-bsdcpio \
      --disable-static
  make
}

package() {
  cd "libarchive-${pkgver}"

  make DESTDIR="$pkgdir" install
  sed -i "s/iconv //" "$pkgdir"/$GLIBC_PREFIX/lib/pkgconfig/libarchive.pc
  install -Dm0644 COPYING "$pkgdir/$GLIBC_PREFIX/share/licenses/libarchive/COPYING"
  rm -fr "$pkgdir/$GLIBC_PREFIX/glibc/bin"
}
