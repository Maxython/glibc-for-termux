# The original PKGBUILD was taken from the gitlab.archlinux.org/archlinux/packaging repo:
# - https://gitlab.archlinux.org/archlinux/packaging/packages/expat/-/blob/main/PKGBUILD

pkgname=expat-glibc
pkgver=2.5.0
pkgrel=0
pkgdesc="An XML parser library"
arch=(any)
url="https://libexpat.github.io/"
license=(MIT)
source=(https://github.com/libexpat/libexpat/releases/download/R_${pkgver//./_}/expat-$pkgver.tar.bz2)
sha512sums=('22fd904d75aab7506a85c03519b9cf79e44898c8e1ba6abf6cb7f95de71b8e63a7e2d5cf4395e3627d46035ea29342b3e631a8809fef8aad3f59976dc075ad17')
b2sums=('15a5dcd3af17995fb4299301710b38d609c1fe7a8d6a6284581fedd96e89e0c16526d0342fb55773ac9d678cd65dc5cdb1532c764eeb3a20ccdf1e168b96e337')
groups=('gpkg-dev')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=$GLIBC_PREFIX \
        -DCMAKE_BUILD_TYPE=None \
        -W no-dev \
        -B build \
        -S expat-$pkgver
  make VERBOSE=1 -C build
}

package() {
  make VERBOSE=1 DESTDIR="$pkgdir" -C build install
  install -vDm 644 expat-$pkgver/COPYING -t "$pkgdir/$GLIBC_PREFIX/share/licenses/$pkgname/"
  install -vDm 644 expat-$pkgver/{AUTHORS,Changes,README.md} -t "$pkgdir/$GLIBC_PREFIX/share/doc/$pkgname/"
}
