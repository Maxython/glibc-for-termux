# The original PKGBUILD was taken from the gitlab.archlinux.org/archlinux/packaging repo:
# - https://gitlab.archlinux.org/archlinux/packaging/packages/bzip2/-/blob/main/PKGBUILD

pkgname=bzip2-glibc
pkgver=1.0.8
pkgrel=0
pkgdesc="A high-quality data compression program"
arch=('any')
license=('BSD')
url="https://sourceware.org/bzip2/"
source=(https://sourceware.org/pub/bzip2/bzip2-$pkgver.tar.gz
        bzip2.pc)
sha256sums=('ab5a03176ee106d3f0fa90e381da478ddae405918153cca248e682cd0c4a2269'
            '0cb81972e1bda1d0d48ca7a2764c9aec3d6640385c1842f9b710f69d26e9c559')
groups=('gpkg-dev')

prepare() {
  cd bzip2-$pkgver
  cp ../bzip2.pc bzip2.pc
  sed "s|@VERSION@|$pkgver|" -i bzip2.pc
}

build() {
  cd bzip2-$pkgver

  make -f Makefile-libbz2_so CC="gcc $CFLAGS $CPPFLAGS $LDFLAGS"
  make bzip2 bzip2recover CC="gcc $CFLAGS $CPPFLAGS $LDFLAGS"
}

package() {
  cd bzip2-$pkgver

  install -dm755 "$pkgdir"/$GLIBC_PREFIX/{bin,lib,include,share/man/man1}

  install -m755 bzip2-shared "$pkgdir"/$GLIBC_PREFIX/bin/bzip2
  install -m755 bzip2recover bzdiff bzgrep bzmore "$pkgdir"/$GLIBC_PREFIX/bin
  ln -sf bzip2 "$pkgdir"/$GLIBC_PREFIX/bin/bunzip2
  ln -sf bzip2 "$pkgdir"/$GLIBC_PREFIX/bin/bzcat

  cp -a libbz2.so* "$pkgdir"/$GLIBC_PREFIX/lib
  ln -s libbz2.so.$pkgver "$pkgdir"/$GLIBC_PREFIX/lib/libbz2.so
  ln -s libbz2.so.$pkgver "$pkgdir"/$GLIBC_PREFIX/lib/libbz2.so.1 # For compatibility with some other distros

  install -m644 bzlib.h "$pkgdir"/$GLIBC_PREFIX/include/

  install -m644 bzip2.1 "$pkgdir"/$GLIBC_PREFIX/share/man/man1/
  ln -sf bzip2.1 "$pkgdir"/$GLIBC_PREFIX/share/man/man1/bunzip2.1
  ln -sf bzip2.1 "$pkgdir"/$GLIBC_PREFIX/share/man/man1/bzcat.1
  ln -sf bzip2.1 "$pkgdir"/$GLIBC_PREFIX/share/man/man1/bzip2recover.1

  install -Dm644 bzip2.pc -t "$pkgdir"/$GLIBC_PREFIX/lib/pkgconfig
  install -Dm644 LICENSE "$pkgdir"/$GLIBC_PREFIX/share/licenses/${pkgname}/LICENSE
}
