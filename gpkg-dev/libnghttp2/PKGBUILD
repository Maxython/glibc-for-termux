# The original PKGBUILD was taken from the gitlab.archlinux.org/archlinux/packaging repo:
# - https://gitlab.archlinux.org/archlinux/packaging/packages/libnghttp2/-/blob/main/PKGBUILD

pkgname=libnghttp2-glibc
pkgver=1.55.1
pkgrel=0
pkgdesc='Framing layer of HTTP/2 is implemented as a reusable C library'
arch=(any)
url='https://nghttp2.org/'
license=(MIT)
source=(https://github.com/nghttp2/nghttp2/releases/download/v$pkgver/nghttp2-$pkgver.tar.xz)
sha512sums=('ad948aa957ce429346b2a79933ba3b5f94e9386e1bc36961ed181685cd65f3d9d6937e3d4d228be17ac793e438776515b6a071ae1801b1481db9ad2c1166c1c3')
groups=('gpkg-dev')

build() {
  cd nghttp2-$pkgver

  autoreconf -i
  ./configure \
    --build=$GPKG_DEV_TARGET \
    --host=$GPKG_DEV_TARGET \
    --target=$GPKG_DEV_TARGET \
    --prefix=$GLIBC_PREFIX \
    --disable-examples \
    --disable-python-bindings \
    --enable-lib-only
  make
}

package() {
  cd nghttp2-$pkgver/lib

  make DESTDIR="$pkgdir" install
  install -Dm644 ../COPYING "$pkgdir/$GLIBC_PREFIX/share/licenses/libnghttp2/COPYING"
}
