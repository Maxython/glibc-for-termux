# The original PKGBUILD was taken from the gitlab.archlinux.org/archlinux/packaging repo:
# - https://gitlab.archlinux.org/archlinux/packaging/packages/libnghttp2/-/blob/main/PKGBUILD

pkgname=libnghttp2-glibc
pkgver=1.54.0
pkgrel=0
pkgdesc='Framing layer of HTTP/2 is implemented as a reusable C library'
arch=(any)
url='https://nghttp2.org/'
license=(MIT)
source=(https://github.com/nghttp2/nghttp2/releases/download/v$pkgver/nghttp2-$pkgver.tar.xz)
sha512sums=('b56a599f8be31e0518902a1dde50c48b599f57a24b382b2fea1ba8d855d1410b8e97b7dcd9620e3b29c308b7cb04bcd9d8db7034978419a90dbe7f84547afc43')
groups=('gpkg-dev')

build() {
  cd nghttp2-$pkgver

  autoreconf -i
  ./configure \
    --host=$GPKG_DEV_TARGET \
    --target=$GPKG_DEV_TARGET \
    --prefix=$GLIBC_PREFIX \
    --disable-examples \
    --disable-python-bindings \
    --enable-lib-only
  make
}

package() {
  cd nghttp2-$pkgver/lib

  make DESTDIR="$pkgdir" install
  install -Dm644 ../COPYING "$pkgdir/$GLIBC_PREFIX/share/licenses/libnghttp2/COPYING"
}
