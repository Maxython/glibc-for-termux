# The original PKGBUILD was taken from the archlinux/svntogit-packages repo:
# - https://github.com/archlinux/svntogit-packages/blob/packages/libnghttp2/trunk/PKGBUILD

pkgname=libnghttp2-glibc
pkgver=1.52.0
pkgrel=0
pkgdesc='Framing layer of HTTP/2 is implemented as a reusable C library'
arch=(any)
url='https://nghttp2.org/'
license=(MIT)
source=(https://github.com/nghttp2/nghttp2/releases/download/v$pkgver/nghttp2-$pkgver.tar.xz)
sha512sums=('3af1ce13270f7afc8652bd3de71200d9632204617fe04d2be7156d60eeb1a5cc415573677791a399ae03577e8e3256939b1b05d27dbd98dee504d09ec5325d56')
groups=('gpkg-dev')

build() {
  cd nghttp2-$pkgver

  autoreconf -i
  ./configure \
    --prefix=$GLIBC_PREFIX \
    --disable-examples \
    --disable-python-bindings \
    --enable-lib-only
  make
}

package() {
  cd nghttp2-$pkgver/lib

  make DESTDIR="$pkgdir" install
  install -Dm644 ../COPYING "$pkgdir/$GLIBC_PREFIX/share/licenses/libnghttp2/COPYING"
}
