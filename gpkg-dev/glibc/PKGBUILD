# The original PKGBUILD was taken from the gitlab.archlinux.org/archlinux/packaging repo:
# - https://gitlab.archlinux.org/archlinux/packaging/packages/glibc/-/blob/main/PKGBUILD

pkgname=glibc
pkgver=2.37
pkgrel=6
_commit=be176490b818b65b5162c332eb6b581690b16e5c
pkgdesc="GNU C Library"
arch=(any)
url='https://www.gnu.org/software/libc/'
license=('GPL' 'LGPL')
options=(!strip staticlibs)
source=(https://ftp.gnu.org/gnu/libc/glibc-$pkgver.tar.xz
	'_Fork.c.patch'
	'dl-tls_init_tp.c.patch'
	'pthread_create.c.patch'
        'faccessat.c.patch'
        'tst-rseq-disable.c.patch'
        'tst-rseq.h.patch'
        'rseq-internal.h.patch'
        'disable-clone3.patch'
        'locale-gen'
        'locale.gen.txt'
        'sdt-config.h'
        'sdt.h'
	'grun'
	'statx.c.patch'
	'xstat64.c.patch'
	'lxstat64.c.patch'
	'fxstatat64.c.patch'
	'fxstat64.c.patch'
	'fstatat64.c.patch'
	'clock_gettime.c.patch'
        'chmod.c.patch'
        'send.c.patch'
        'setdirs.patch'
        'recv.c.patch'
        'accept.c.patch'
        'shmem-android.h'
        'shmget.c.patch'
        'shmat.c.patch'
        'shmctl.c.patch'
        'shmdt.c.patch')
sha256sums=('2257eff111a1815d74f46856daaf40b019c1e553156c69d48ba0cbfc1bb91a43'
            '74da3d03fb81e2511140411bc8bad2a893258a2465c5eec5ac955dccddf9621d'
            '9223be94da6667f0dff0bf7afb032932dd9a151f23af178faa4d1d7f26c58608'
            'dd0f14f9ccca83424bc2b841346047d16c59d229c1a459fc764e6c67f4fe91ba'
            '6814b1c02455fc482d7ea340112bcca1cdd4e13ca2c16f156654d7a65dbdcda2'
            '27e1ea17f40c57f6bac2e46816d0684170818b58ecced7a83efb8e7f0f038af0'
            'b6286fe9ffd9707d171db5dd53b3c084d7d38402e1a32eb8b9bd455d9a8b5ab6'
            'ec053954ab9e19b850227048a61e622bd15d6a651fd7498e507e908485374299'
            '0f778f5a2d20e8710647370e89c793eb27bbf8ef9824558d0154185c91ecd12c'
            'cdd916c0dc29e5d958ddef5c3530947244fd740ce7645601bfabae53512323b8'
            'd42648cea552ba5353a32e264686e992263289d5cc86207314dffc54ab514981'
            'cdc234959c6fdb43f000d3bb7d1080b0103f4080f5e67bcfe8ae1aaf477812f0'
            '774061aff612a377714a509918a9e0e0aafce708b87d2d7e06b1bd1f6542fe70'
            '20b8869cd114ce32bbcc418d1f18931646ed29a6d713f21fb71a136e026e8c7f'
            'e0f6a51cbd64a30394d6a22e5e14603766ac924e9b6690adb673517821c2015e'
            'e67383acf8935a68353f0f1c786161222fbb08c9255cffa3ccb06565e1401cbc'
            'cd981ed4c8b77ee1551ae5a017dcc9b7891a6078450633eafe528e3984254f40'
            'e6b3bac73759e231cfa0099be3ca50f7b5a379ecdac18a598999e87d51c8f249'
            '6ec88f600395da72987fdc3fcd09ac0b992770201a6a7f1a377445a35f0ea52d'
            'a6ea8efc55de0e26494bf8839ae32d95c36b6956788535a9a8912ad989510f36'
            '18f01ed2c6ed8acb122adc443fd428c8a37bc6a7d518f4e433e2ee462eebd5eb'
            '769b294e226877f12d47ac4ff86ab9246da6e547781071c1ee38e60772484c74'
            'f5b5b6ae3596f6fd617d015ff9c417a35da37c86ae3a49b5b5ce88275ed09f12'
            '46e846c4237ab332ffb64abf46d3a95fd773173346e20a7e2b16f385beaab8d4'
            'b8b573fcb85d59fc11ddef9aebb691b8047d700ea1894784883a1359c6ef94a8'
            '59e711916c946fc961bb8c3a0da5d367a90ffedd31bbf7cf250e9bc1e1e04744'
            '660e83ab93f745b8f10ca6bce4704203233d65d7ac10ce481186c59b31252cb0'
            'd7569b8f5aa1816461cf42e8f290ea861157ebb8da936c6fa472593e681737f2'
            '8a246f04d34253504bae46cf29e3e217c031f987b191de0fb2dafa38437e3809'
            '210e48732cdad2a2b01cff15771fcf12730bdad3f2a93ef772c03c8b57c74cc7'
            'e32f030f1f3d42974c3014e8ee3b78084046642d649d496fab31021a1991b2b7')
groups=('gpkg-dev')

prepare() {
  for i in *.patch; do
    patch -Np1 -i ${srcdir}/$i
  done

  install -Dm644 "${srcdir}/shmem-android.h" "glibc-$pkgver/sysdeps/unix/sysv/linux/shmem-android.h"

  mkdir -p glibc-build
}

build() {
  cd glibc-build
  echo "slibdir=$GLIBC_PREFIX/lib" > configparms
  echo "rtlddir=$GLIBC_PREFIX/lib" >> configparms
  echo "sbindir=$GLIBC_PREFIX/bin" >> configparms
  echo "rootsbindir=$GLIBC_PREFIX/bin" >> configparms

  unset CFLAGS
  ../glibc-$pkgver/configure \
      --prefix=$GLIBC_PREFIX \
      --libdir=$GLIBC_PREFIX/lib \
      --libexecdir=$GLIBC_PREFIX/lib \
      --host=$GPKG_DEV_TARGET \
      --target=$GPKG_DEV_TARGET \
      --with-bugurl=https://github.com/termux-pacman/glibc-packages/issues \
      --enable-bind-now \
      --enable-cet \
      --enable-multi-arch \
      --enable-stack-protector=strong \
      --enable-systemtap \
      --disable-profile \
      --disable-crypt \
      --disable-werror CFLAGS="-O2 -pipe"

  echo "build-programs=no" >> configparms
  make -O

  sed -i "/build-programs=/s#no#yes#" configparms
  echo "CFLAGS=-O2 -pipe" >> configparms
  make -O
  make info

  localedef -c -f ../glibc-$pkgver/localedata/charmaps/UTF-8 -i ../glibc-$pkgver/localedata/locales/C ../C.UTF-8/
}

package() {
  make -C glibc-build install_root="$pkgdir" install
  rm -f "$pkgdir"/$GLIBC_PREFIX/etc/ld.so.{cache,conf}

  # Shipped in tzdata
  rm -f "$pkgdir"/$GLIBC_PREFIX/bin/{tzselect,zdump,zic}

  cd glibc-$pkgver

  install -dm755 "$pkgdir"/$GLIBC_PREFIX/lib/{locale,systemd/system,tmpfiles.d}
  install -m644 nscd/nscd.conf "$pkgdir/$GLIBC_PREFIX/etc/nscd.conf"
  install -m644 nscd/nscd.service "$pkgdir/$GLIBC_PREFIX/lib/systemd/system"
  install -m644 nscd/nscd.tmpfiles "$pkgdir/$GLIBC_PREFIX/lib/tmpfiles.d/nscd.conf"
  install -dm755 "$pkgdir/$GLIBC_PREFIX/var/db/nscd"

  install -m644 posix/gai.conf "$pkgdir"/$GLIBC_PREFIX/etc/gai.conf

  install -m755 "$srcdir/locale-gen" "$pkgdir/$GLIBC_PREFIX/bin"

  # Create /etc/locale.gen
  install -m644 "$srcdir/locale.gen.txt" "$pkgdir/$GLIBC_PREFIX/etc/locale.gen"
  sed -e '1,3d' -e 's|/| |g' -e 's|\\| |g' -e 's|^|#|g' \
    "$srcdir/glibc-$pkgver/localedata/SUPPORTED" >> "$pkgdir/$GLIBC_PREFIX/etc/locale.gen"

  # install C.UTF-8 so that it is always available
  install -dm755 "$pkgdir/$GLIBC_PREFIX/lib/locale"
  cp -r "$srcdir/C.UTF-8" -t "$pkgdir/$GLIBC_PREFIX/lib/locale"
  sed -i '/#C\.UTF-8 /d' "$pkgdir/$GLIBC_PREFIX/etc/locale.gen"

  # Provide tracing probes to libstdc++ for exceptions, possibly for other
  # libraries too. Useful for gdb's catch command.
  install -Dm644 "$srcdir/sdt.h" "$pkgdir/$GLIBC_PREFIX/include/sys/sdt.h"
  install -Dm644 "$srcdir/sdt-config.h" "$pkgdir/$GLIBC_PREFIX/include/sys/sdt-config.h"

  sed -i "s|!/bin/bash|!$TERMUX_PREFIX/bin/bash|" "$pkgdir"/$GLIBC_PREFIX/bin/*
  mkdir -p "$pkgdir"/$TERMUX_PREFIX/bin
  install -m755 "$srcdir/grun" "$pkgdir/$TERMUX_PREFIX/bin"

  replace_hard_with_symbolic "$pkgdir"
}
