# The original PKGBUILD was taken from the gitlab.archlinux.org/archlinux/packaging repo:
# - https://gitlab.archlinux.org/archlinux/packaging/packages/openssl/-/blob/main/PKGBUILD

pkgname=openssl-glibc
pkgver=3.1.1
pkgrel=0
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=(any)
url='https://www.openssl.org'
license=('custom:BSD')
makedepends=('perl')
source=("https://www.openssl.org/source/openssl-${pkgver}.tar.gz"
	'ca-dir.patch'
        '10-main.conf.patch')
sha256sums=('b3aa61334233b852b63ddb048df181177c2c659eb9d4376008118f9c08d07674'
            '326478a21829b79710ce0d8f7437e3be10c542471fef3bdf5f70ac232a5bf9e8'
            '7d50a1d9df32b6e0f2f0e5dc00418663f22d05f5643cfde34e4fea9bffb4b51b')
groups=('gpkg-dev')

prepare() {
	if [ "${CARCH}" = "armv7h" ]; then
		patch -Np1 -i "$srcdir/10-main.conf.patch"
	fi

	cd "$srcdir/openssl-$pkgver"

	# set ca dir to /etc/ssl by default
	patch -Np1 -i "$srcdir/ca-dir.patch"
}

build() {
	cd "$srcdir/openssl-$pkgver"

	if [ "${CARCH}" = "armv7h" ]; then
		openssltarget='linux-armv4'
		optflags=''
	elif [ "${CARCH}" = "aarch64" ]; then
		openssltarget='linux-aarch64'
		optflags='no-afalgeng'
	elif [ "${CARCH}" = "x86_64" ]; then
		openssltarget='linux-x86_64'
		optflags='enable-ec_nistp_64_gcc_128'
	elif [ "${CARCH}" = "i686" ]; then
		openssltarget='linux-generic32'
		optflags='no-sse2'
	fi

	export PATH=/usr/bin/core_perl:$PATH

	# mark stack as non-executable: http://bugs.archlinux.org/task/12434
	./Configure --prefix=$GLIBC_PREFIX --openssldir=$GLIBC_PREFIX/etc/ssl --libdir=lib \
		shared enable-ktls ${optflags} "${openssltarget}" \
		"-Wa,--noexecstack ${CPPFLAGS} ${CFLAGS} ${LDFLAGS}"

	make depend
	make
}

package() {
	cd "$srcdir/openssl-$pkgver"

	make DESTDIR="$pkgdir" MANDIR=$GLIBC_PREFIX/share/man MANSUFFIX=ssl install_sw install_ssldirs install_man_docs

	install -D -m644 LICENSE.txt "$pkgdir/$GLIBC_PREFIX/share/licenses/$pkgname/LICENSE"

	ln -s "$TERMUX_PREFIX/etc/resolv.conf" "$pkgdir/$GLIBC_PREFIX/etc"
	ln -s "$TERMUX_PREFIX/etc/tls/cert.pem" "$pkgdir/$GLIBC_PREFIX/etc/ssl/certs/ca-certificates.crt"
}
